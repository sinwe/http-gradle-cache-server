buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
        bintrayPluginVersion = '1.7.3'
        releasePluginVersion = '2.7.0'
        owaspPluginVersion = '3.2.1'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPluginVersion"
        classpath "net.researchgate:gradle-release:$releasePluginVersion"
        classpath "org.owasp:dependency-check-gradle:$owaspPluginVersion"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'net.researchgate.release'
apply plugin: 'org.owasp.dependencycheck'

group = 'com.github.sinwe.gradle.caching'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("net.openhft:chronicle-map:3.15.1")
    compile("com.hazelcast:hazelcast-client")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.projectreactor:reactor-test')
}

jar {
    version = "${project.version}"
}

ext.bintrayUsername = hasProperty("bintrayUser") ? bintrayUser : System.getenv("ossrhUsername")
ext.bintrayKey = hasProperty("bintrayApiKey") ? bintrayApiKey : System.getenv("ossrhPassword")

bintray {
    user = "$bintrayUsername"
    key = "$bintrayKey"
    publications = ['MyPublication']
    configurations = ['archives']
    pkg {
        repo = 'sinwe-maven'
        name = 'http-gradle-cache-server'
        version {
            name = "${project.version}"
            released = new Date()
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId "${project.group}"
            artifactId "${project.name}"
            version "${project.version}"
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload